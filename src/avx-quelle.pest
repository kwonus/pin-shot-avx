// AVX-Quelle Grammar - Expressed in PEG
// Copyright (c) 2024, Kevin Wonus
// MIT License

// Short summary:
// command has multiple clauses and optional export/scope/macro support
// each clause is a standalone unit.  Search-Clauses have more granular units/syntax
// search-segment contains zero or one expression and/or any number of assignments
// expression contains [>= 1] fragments
// fragments contain [>= 1] features
// fragments are used to match terms (i.e. word tokens) across the corpus being searched; match logic should consider all features of the fragment/token when deeming it to be matched
//

WHITESPACE = _ { " " | "\t" | "\r" | "\n" }
statement = { avx_rev | singleton | implicits }
implicits = { segment ~ additional_segment* }
AVX_QUERY = _{ ^"_AVX_REV_" ~  "=?" }
avx_rev = { AVX_QUERY ~ "4.3.03" }   // grammar rule allows clients to assert expected version compatibility via extern C function call

segment = { elements? ~ filter* ~ apply_macro? }
elements = _{ (expression ~ controls?) | (controls ~ expression?) }
expression = { search | hashtag_label | hashtag_id }
singleton = { exit | help | history | review | delete | absorb | get | set | clear | print | invoke_id }

export = { (output | append | overwrite) }
additional_segment = _{ SEARCH_EXPLICIT_BOUNDARY ~ segment }
filter = { FILTER_OP ~ (bookName | bookNum) ~ (verse_range | chapter_range | verse | chapter)? }

item = { (wildcard | text | pos | pn_pos12 | pos32 | nupos | greek | hebrew | loc | seg | lemma | punc | decoration | delta | entity ) }
fragment = { feature_option ~ (AND ~ feature_option)* }
feature_option = { feature ~ (OR ~ feature)* }
feature = { not? ~ item}

entity = _{ "[" ~ entities ~ "]" }
entities = @{ ^"person" | ^"man" | ^"woman" | ^"tribe" | ^"city" | ^"river" | ^"mountain" | ^"animal" | ^"gemstone" | ^"measurement" | ^"any" | ^"any_Hitchcock" }

delta = { ^"/delta/" }

pn = { ^"/1p/" | ^"/2p/" | ^"/3p/" | "/indefinite/" }
noun = { ^"/noun/" | ^"/n/" }
verb = { ^"/verb/" | ^"/v/" }
adj = { ^"/adjective/" | ^"/adj/" }
adv = { ^"/adverb/" | ^"/adv/" }
det = { ^"/determiner/" | ^"/det/" }
art = { ^"/particle/" | ^"/part/" }

most = { noun | verb | adj | adv | det | art }

pro = { ^"/pronoun/" | ^"/pn/" }

pro_neuter = { ^"/pn_neuter/" }
pro_masculine = { ^"/pn_masculine/" | ^"/pn_male/" }
pro_feminine = { ^"/pn_feminine/" | ^"/pn_fem/" }

noun_genitive  = { ^"/noun_genitive/"   | ^"/p_gen/" }
any_genitive   = { ^"/genitive/"      | ^"/any_gen/" }
other_genitive =_{ noun_genitive      | any_genitive }

pro_genitive   = { ^"/pn_genitive/"   | ^"/pn_gen/" }
pro_nominative = { ^"/pn_nominative/" | ^"/pn_nom/" }
pro_objective  = { ^"/pn_objective/"  | ^"/pn_obj/" }
pro_reflexive  = { ^"/pn_reflexive/"  | ^"/pn_rfx/" }

pro_all =_{ pro | pro_neuter | pro_masculine | pro_feminine | pro_genitive | pro_nominative | pro_objective | pro_reflexive }

prep = { ^"/preposition/" | ^"/prep/" }
inter = { ^"/interjection/" | ^"/inter/" }
conj = { ^"/conjunction/" | ^"/conj/" }
num = { ^"/numeric/" | ^"/num/" }

more =_{ prep | inter | conj | num | other_genitive }

wh = { ^"/wh/" }
singular = { ^"/singular/" }
plural = { ^"/pural/" }
pos = { most | more | pro_all | wh | singular | plural | pn }

loc_bob = { ^"/BoB/" }
loc_eob = { ^"/EoB/" }
loc_boc = { ^"/BoC/" }
loc_eoc = { ^"/EoC/" }
loc_bov = { ^"/BoV/" }
loc_eov = { ^"/EoV/" }
loc = { loc_bob | loc_eob | loc_boc | loc_eoc | loc_bov | loc_eov }

seg_hsm = { ^"/Hsm/"  }
seg_csm = { ^"/Csm/"  }
seg_ssm = { ^"/Ssm/"  }
seg_any = { ^"/sm/"   }
seg = { seg_hsm | seg_csm | seg_ssm | seg_any }

punc_any      = { "/_/" }
punc_exclaim  = { "/!/" }
punc_question = { "/?/" }
punc_declare  = { "/./" }
punc_dash     = { "/-/" }
punc_semi     = { "/;/" }
punc_comma    = { "/,/" }
punc_colon    = { "/:/" }
punc_possessive = { "/\'/" }
punc_parenthetical = { "/(/" }
punc_parenthetical_close = { "/)/" }
punc = { punc_any | punc_question | punc_declare | punc_dash | punc_semi | punc_comma | punc_colon | punc_possessive | punc_parenthetical | punc_parenthetical_close }

italics = { ^"/italics/" }
jesus = { ^"/Jesus/" }

print = { SINGLETON ~ ^"print" ~ export? }
exit = { SINGLETON ~ ^"exit" }
help = { SINGLETON ~ ^"help" ~ topic? }

topic = ${ SINGLETON? ~ (root_topics | group_topics) }
reset = { SINGLETON ~ delete_keyword ~ review_flag_history ~ any_range }
delete = { SINGLETON ~ delete_keyword ~ (label | (review_flag_labels ~ ((wildcard ~ date_range*) | date_range ~ wildcard*)) ) }
delete_keyword = _{ ^"delete" }
review = { SINGLETON ~ review_keyword ~ (label | (review_flag_labels ~ ((wildcard ~ date_range*) | date_range ~ wildcard*))* ) }
review_flag_labels = _ { ^"-labels" }
history_full_reset = { "-all" }
history = { SINGLETON ~ review_keyword ~ (id | (review_flag_history ~ any_range* ) ) }
review_flag_history = _ { ^"-history" }
review_keyword = _{ ^"review" }
absorb = { SINGLETON ~ ^"absorb" ~ (label | id) }
clear = { SINGLETON ~ ^"clear" ~ (var | all_key) }
var = { span_key | lexicon_key | lexicon_search_key | lexicon_render_key | match_key | match_lemma_key | match_word_key | format_key }
set = { span_set | lexicon_set | lexicon_search_set | lexicon_render_set | match_set | match_lemma_set | match_word_set | format_set }
get = { span_get | lexicon_get | lexicon_search_get | lexicon_render_get | match_get | match_lemma_get | match_word_get | format_get | version_get | all_get }

search = { ordered | unordered }   // quoted-searches are ordered // unquoted-searches are unordered
unordered = { fragment+ } // Unordered expression
ordered = { QUOTE ~ fragment ~ (unanchored | fragment)* ~ QUOTE } // Quoted/Ordered expression
unanchored = { ellipsis ~ fragment }

span_key = @{ ^"span" }
SPAN_VAR = _{ span_key }
span_set = { (SET | SINGLETON) ~ SPAN_VAR ~ EQUALS ~ span_option }
span_var = { SPAN_VAR ~ ASSIGN ~ span_option }
span_get = { (GET | SINGLETON) ~ SPAN_VAR }
span_option = _{ DIGIT_le3 | span_verse }
span_verse = ${ ^"verse" }

lexicon_key = @{ ^"lexicon" }
LEXICON_VAR = _{ lexicon_key }
lexicon_set = { (SET | SINGLETON) ~ LEXICON_VAR ~ EQUALS ~ lexicon_option }
lexicon_var = { LEXICON_VAR ~ ASSIGN ~ lexicon_option }
lexicon_get = { (GET | SINGLETON) ~ LEXICON_VAR }

lexicon_search_set = { (SET | SINGLETON) ~ LEXICON_SEARCH_VAR ~ EQUALS ~ lexicon_option }
lexicon_search_var = { LEXICON_SEARCH_VAR ~ ASSIGN ~ lexicon_option }
lexicon_search_get = { (GET | SINGLETON) ~ LEXICON_SEARCH_VAR }

lexicon_render_set = { (SET | SINGLETON) ~ LEXICON_RENDER_VAR ~ EQUALS ~ lexicon_option }
lexicon_render_var = { LEXICON_RENDER_VAR ~ ASSIGN ~ lexicon_option }
lexicon_render_get = { (GET | SINGLETON) ~ LEXICON_RENDER_VAR }

lexicon_option = _{ LEXICONS }
LEXICONS = _{ AVX | AV | DUAL }  // very specific domain for the AVX driver/parser implementation
AVX = ${ ^"modern" | "^avx" }
AV = ${ ^"kjv" | ^"av" }
DUAL = ${ ^"dual" | ^"both" }

match_key = @{ ^"similarity" }
MATCH_VAR = _{ match_key }
match_set = { (SET | SINGLETON) ~ MATCH_VAR ~ EQUALS ~ similarity_opt }
match_var = { MATCH_VAR ~ ASSIGN ~ similarity_opt }
match_get = { (GET | SINGLETON) ~ MATCH_VAR }

match_word_set = { (SET | SINGLETON) ~ MATCH_WORD_VAR ~ EQUALS ~ similarity_opt }
match_word_var = { MATCH_WORD_VAR ~ ASSIGN ~ similarity_opt }
match_word_get = { (GET | SINGLETON) ~ MATCH_WORD_VAR }

match_lemma_set = { (SET | SINGLETON) ~ MATCH_LEMMA_VAR ~ EQUALS ~ similarity_opt }
match_lemma_var = { MATCH_LEMMA_VAR ~ ASSIGN ~ similarity_opt }
match_lemma_get = { (GET | SINGLETON) ~ MATCH_LEMMA_VAR }

similarity_opt = ${ EXACT | PRECISE | FUZZY }

PRECISE = ${ ^"precise" | ("100" ~ PERCENT) }
EXACT = ${ ^"exact" | "0" }
THIRTY_SOMETHING = ${ "3" ~ ('3' .. '9') }
FORTY_PLUS = ${ ('4' .. '9') ~ ('0' .. '9') }

FUZZY = ${ (THIRTY_SOMETHING | FORTY_PLUS) ~ PERCENT } // legal thresholds are from 33 to 99 (100% is excluded because that would represent an exact-match

format_key = @{ ^"format" }
FORMAT_VAR = _{ format_key }
format_set = { (SET | SINGLETON) ~ FORMAT_VAR ~ EQUALS ~ format_option }
format_var = { FORMAT_VAR ~ ASSIGN ~ format_option }
format_get = { (GET | SINGLETON) ~ FORMAT_VAR }
format_option = _{ html | markdown | textual | json | yaml }
html = ${ ^"html" }
markdown = ${ ^"markdown" | ^"md" }
textual = ${ ^"text" | ^"utf8" }
json = ${ ^"json" }
yaml = ${ ^"yaml" }

all_key = @{ ^"all" }
ALL_VAR = _{ all_key }
all_var = {ALL_VAR ~ ASSIGN ~ all_option }
all_get = { (GET | SINGLETON) ~ ALL_VAR }
all_option = _{ current | defaults }

book = { (bookName | bookNum) }
verse = ${ chapter ~ SEPERATOR ~ DIGIT_byte }
verse_range = ${ SEPERATOR ~ DIGIT_byte ~ HYPHEN ~ DIGIT_byte}
chapter = ${ SEPERATOR ~ DIGIT_byte }
chapter_range = ${ SEPERATOR ~ DIGIT_byte ~ HYPHEN ~ DIGIT_byte}
bookName = ${ ALPHA+ | (onetwothree ~ ALPHA+) }
onetwothree = { ('1' .. '3') }
bookNum  = ${ ('1' .. '9') | (('1' .. '5') ~ DIGIT) ~ ( "6" ~ ('1' .. '6')) } // 1-66
controls = { OPEN_BRACE ~ var_opt* ~ (apply_macro ~ var_opt*)? ~ CLOSE_BRACE }
var_opt = { span_var | lexicon_var | lexicon_search_var | lexicon_render_var | match_var | match_lemma_var | match_word_var | format_var | all_var }
get_opt = { span_get | lexicon_get | lexicon_search_get | lexicon_render_get | match_get | match_lemma_get | match_word_get | format_get | version_get | all_get }
apply_macro = { "||" ~ hashtag_label }

label_parameter = _{ label | wildcard }
hashtag_label = ${ POUND ~ label }
historic_parameter = _{ historic_sequence | hashtag_id | id_range }
historic_sequence = ${ id ~ HYPHEN ~ id }
hashtag_id = ${ POUND ~ id }
invoke_id = ${ SINGLETON ~ id }
id = ${ ('1' .. '9') ~ ( '0' .. '9')* }

decoration = { italics | jesus }
pos32 = ${ "#" ~ HEX32 }
pn_pos12 = ${ "#" ~ HEX16 }
greek = ${ DIGITS ~ ^":G" }
hebrew = ${ DIGITS ~ ^":H" }

lemma = { "\\" ~ text ~ "\\" }

wildcard = ${ (term_begin | STAR) ~ term_contains* ~ term_end? }

ellipsis = _{ "..." }

append = { ">>" ~  filename }
output = { ">" ~ filename }
overwrite = { "=>" ~ filename }

FILESPEC = _{ ('a' .. 'z' | 'A' .. 'Z' | DIGIT) | "/" | "\\" | "-" | "_" | ":" }
quoted_filespec = ${ QUOTE ~ FILESPEC ~ (FILESPEC | " ") + ~ QUOTE }
unquoted_legal = ${ FILESPEC+ }
filename = { quoted_filespec | unquoted_legal }

// SEARCH
TOPIC_FIND = @{ ^"FIND" } // 1
TOPIC_FILTER = @{ ^"FILTER" } // 2
// CONTROL
TOPIC_SET = @{ ^"SET" } // 3
TOPIC_CLEAR = @{ ^"CLEAR" } // 4
TOPIC_OPTION = @{ ^"OPTION" } // NEW
TOPIC_GET = @{ ^"GET" } // 5
TOPIC_RESET = @{ ^"RESET" } // 6
TOPIC_ABSORB = @{ ^"ABSORB" } // 7
//OUTPUT
TOPIC_PRINT = @{ ^"PRINT" } // 8
TOPIC_EXPORT = @{ ^"EXPORT" } // 9
// SYSTEM
TOPIC_HELP = @{ ^"HELP" } // 10
TOPIC_VERSION = @{ ^"VERSION" } // 11
TOPIC_EXIT = @{ ^"EXIT" } // 12
// HISTORY & LABELING
TOPIC_INVOKE = @{ ^"INVOKE" } // 13
TOPIC_APPLY = @{ ^"APPLY" }   // 14
TOPIC_DELETE = @{ ^"DELETE" } // 10
TOPIC_REVIEW = @{ ^"REVIEW" } // 16
TOPIC_INIT = @{ ^"INITIALIZE" } // 17 **
root_topics = { TOPIC_FIND | TOPIC_FILTER | TOPIC_SET | TOPIC_CLEAR | TOPIC_OPTION | TOPIC_GET | TOPIC_RESET | TOPIC_ABSORB | TOPIC_PRINT | TOPIC_EXPORT | TOPIC_HELP | TOPIC_VERSION | TOPIC_EXIT | TOPIC_INVOKE | TOPIC_APPLY | TOPIC_DELETE | TOPIC_REVIEW | TOPIC_INIT }

GROUP_TOPIC_SEARCH = @{ ^"SEARCH" }
GROUP_TOPIC_CONTROL = @{ ^"CONTROL" | ^"SETTINGS" }
GROUP_TOPIC_OUTPUT = @{ ^"OUTPUT" }
GROUP_TOPIC_SYSTEM = @{ ^"SYSTEM" }
GROUP_TOPIC_HISTORY_AND_LABELING = @{ ^"HISTORY" | ^"MACRO" | ^"LABEL" | ^"LABELING" | ^"LABELLING" }
group_topics = { GROUP_TOPIC_SYSTEM | GROUP_TOPIC_OUTPUT | GROUP_TOPIC_CONTROL | GROUP_TOPIC_SEARCH | GROUP_TOPIC_HISTORY_AND_LABELING }

nupos = {
	^"#a-acp" |
	^"#av" |
	^"#av-an" |
	^"#av-c" |
	^"#avc-jn" |
	^"#av-d" |
	^"#av-dc" |
	^"#av-ds" |
	^"#av-dx" |
	^"#av-j" |
	^"#av-jc" |
	^"#av-jn" |
	^"#av-js" |
	^"#av-n1" |
	^"#av-s" |
	^"#avs-jn" |
	^"#av-vvg" |
	^"#av-vvn" |
	^"#av-x" |
	^"#c-acp" |
	^"#cc" |
	^"#cc-acp" |
	^"#c-crq" |
	^"#ccx" |
	^"#crd" |
	^"#cs" |
	^"#cst" |
	^"#d" |
	^"#dc" |
	^"#dg" |
	^"#ds" |
	^"#dt" |
	^"#dx" |
	^"#fw-es" |
	^"#fw-fr" |
	^"#fw-ge" |
	^"#fw-gr" |
	^"#fw-it" |
	^"#fw-la" |
	^"#fw-mi" |
	^"#j" |
	^"#j-av" |
	^"#jc" |
	^"#jc-jn" |
	^"#jc-vvg" |
	^"#jc-vvn" |
	^"#j-jn" |
	^"#j-u" |
	^"#j-vvnu" |
	^"#jp" |
	^"#js" |
	^"#js-jn" |
	^"#js-vvg" |
	^"#js-vvn" |
	^"#j-vvg" |
	^"#j-vvn" |
	^"#n1" |
	^"#n1-an" |
	^"#n1-j" |
	^"#n11vbz" |
	^"#n2" |
	^"#n2-acp" |
	^"#n2-an" |
	^"#n2-av" |
	^"#n2-dx" |
	^"#n2-j" |
	^"#n2-jn" |
	^"#n2-vdg" |
	^"#n2-vhg" |
	^"#n2-vvg" |
	^"#n2-vvn" |
	^"#ng1" |
	^"#ng1-an" |
	^"#ng1-j" |
	^"#ng1-jn" |
	^"#ng1-vvn" |
	^"#ng2" |
	^"#ng2-jn" |
	^"#n-jn" |
	^"#njp" |
	^"#njp2" |
	^"#njpg1" |
	^"#njpg2" |
	^"#np1" |
	^"#np2" |
	^"#npg1" |
	^"#npg2" |
	^"#np-n1" |
	^"#np-n2" |
	^"#np-ng1" |
	^"#np1-n" |
	^"#n-vdg" |
	^"#n-vhg" |
	^"#n-vvg" |
	^"#n-vvn" |
	^"#ord" |
	^"#p-acp" |
	^"#pc-acp" |
	^"#pi" |
	^"#pi2" |
	^"#pi2x" |
	^"#pig" |
	^"#pigx" |
	^"#pix" |
	^"#pn22" |
	^"#pn31" |
	^"#png11" |
	^"#png12" |
	^"#png21" |
	^"#png22" |
	^"#png31" |
	^"#png32" |
	^"#pno11" |
	^"#pno12" |
	^"#pno21" |
	^"#pno31" |
	^"#pno32" |
	^"#pns11" |
	^"#pns12" |
	^"#pns21" |
	^"#pns31" |
	^"#pns32" |
	^"#po11" |
	^"#po12" |
	^"#po21" |
	^"#po22" |
	^"#po31" |
	^"#po32" |
	^"#pp" |
	^"#pp-f" |
	^"#px11" |
	^"#px12" |
	^"#px21" |
	^"#px22" |
	^"#px31" |
	^"#px32" |
	^"#pxg21" |
	^"#q-crq" |
	^"#r-crq" |
	^"#ro-crq" |
	^"#qo-crq" |
	^"#rg-crq" |
	^"#n-crq" |
	^"#sy" |
	^"#uh" |
	^"#uh-av" |
	^"#uh-crq" |
	^"#uh-dx" |
	^"#uh-j" |
	^"#uh-jn" |
	^"#uh-np" |
	^"#uh-n" |
	^"#uh-v" |
	^"#uh-x" |
	^"#vb2" |
	^"#vb2-imp" |
	^"#vb2r" |
	^"#vb2x" |
	^"#vbb" |
	^"#vbbx" |
	^"#vbd" |
	^"#vbds" |
	^"#vbd2s" |
	^"#vbdr" |
	^"#vbd2r" |
	^"#vbd2" |
	^"#vbd2x" |
	^"#vbdp" |
	^"#vbdx" |
	^"#vbg" |
	^"#vbi" |
	^"#vbm" |
	^"#vbmx" |
	^"#vbn" |
	^"#vbp" |
	^"#vbr" |
	^"#vbz" |
	^"#vbzx" |
	^"#vd2" |
	^"#vd2-imp" |
	^"#vd2x" |
	^"#vdb" |
	^"#vdbx" |
	^"#vdd" |
	^"#vdd2" |
	^"#vdd2x" |
	^"#vddp" |
	^"#vddx" |
	^"#vdg" |
	^"#vdi" |
	^"#vdn" |
	^"#vdp" |
	^"#vdz" |
	^"#vdzx" |
	^"#vh2" |
	^"#vh2-imp" |
	^"#vh2x" |
	^"#vhb" |
	^"#vhbx" |
	^"#vhd" |
	^"#vhd2" |
	^"#vhdp" |
	^"#vhdx" |
	^"#vhg" |
	^"#vhi" |
	^"#vhn" |
	^"#vhp" |
	^"#vhz" |
	^"#vhzx" |
	^"#vm2" |
	^"#vm2x" |
	^"#vmb" |
	^"#vmb1" |
	^"#vmbx" |
	^"#vmd" |
	^"#vm2" |
	^"#vmd2" |
	^"#vmd2x" |
	^"#vmdp" |
	^"#vmdx" |
	^"#vmi" |
	^"#vmn" |
	^"#vmp" |
	^"#vv2" |
	^"#vv2-imp" |
	^"#vv2x" |
	^"#vvb" |
	^"#vvbx" |
	^"#vvd" |
	^"#vvd2" |
	^"#vvd2x" |
	^"#vvdp" |
	^"#vvdx" |
	^"#vvg" |
	^"#vvi" |
	^"#vvn" |
	^"#vvp" |
	^"#vvz" |
	^"#vvzx" |
	^"#xx" |
	^"#zz" }

match_word_key = @{ ^"word" }
match_lemma_key = @{ ^"lemma" }
MATCH_LEMMA_VAR = _{ match_lemma_key | (match_key ~ "." ~ match_lemma_key ) }
MATCH_WORD_VAR = _{ match_word_key | (match_key ~ "." ~ match_word_key ) }

lexicon_search_key = @{ ^"word" }
lexicon_render_key = @{ ^"lemma" }
LEXICON_SEARCH_VAR = _{ lexicon_search_key | (lexicon_key ~ "." ~ lexicon_search_key ) }
LEXICON_RENDER_VAR = _{ lexicon_render_key | (lexicon_key ~ "." ~ lexicon_render_key ) }

any_range = _{ (id_range ~ date_range*) | (date_range ~ id_range*) }
id_range = _{ (id_from | id_until) | (id_until | id_from) }
id_from = { ^"from" ~ id }
id_until = { ^"until" ~ id }

date_range = _{ (date_from | date_until) | (date_until | date_from) }
date_from = { ^"from" ~ history_date }
date_until = { ^"until" ~ history_date }
history_date = ${ "20" ~ DIGIT ~ DIGIT ~ "/" ~ DIGIT ~ DIGIT? ~ "/" ~ DIGIT ~ DIGIT? }

DIGIT = _{ '0' .. '9' }
HEX = _{ DIGIT | ('a' .. 'f') | ('A' .. 'F') }
DIGITS = ${ DIGIT+ }
DIGIT_ge5 = ${DIGIT ~ DIGIT ~ DIGIT ~ DIGIT ~ DIGIT+ }
DIGIT_le2 = ${DIGIT ~ DIGIT? }
DIGIT_le3 = ${DIGIT ~ DIGIT ~ DIGIT? }
DIGIT_byte = ${ (("0" | "1" | "2") ~ DIGIT ~ DIGIT) | (DIGIT ~ DIGIT?) }
HEX16 = ${ HEX ~ HEX ~ HEX ~ HEX }
HEX32 = ${ HEX ~ HEX ~ HEX ~ HEX ~ HEX ~ HEX ~ HEX ~ HEX }
AND = _{ "&" }

label = ${ LABEL_FIRST_CHAR ~ LABEL_CHAR* }
LABEL_CHAR = _{ (DIGIT | "-" | "_" | LABEL_FIRST_CHAR) }
LABEL_FIRST_CHAR = _{ ( 'a' .. 'z' | 'A' .. 'Z') }

version_get = { (GET | SINGLETON) ~ VERSION_VAR }
version_key = @{ ^"version" }
VERSION_VAR = _{ version_key }

OR = _{ "|" }
SEARCH_EXPLICIT_BOUNDARY = _{ "+" }
OPEN_BRACE = _{ "[" }
CLOSE_BRACE = _{ "]" }
POUND = _{ "#" }
FILTER_OP = _{ "<" }
QUOTE = _{ "\"" }
ASSIGN = _{ ":" }
SEPERATOR = _{ ":" }
EQUALS = _{ "=" }
STAR = _{ "*" }
SPACE = _{ " " }
PERCENT = _{ "%" }
SET = _{ SINGLETON ~ set_command }
set_command = ${ ^"set" }
GET = _{ SINGLETON ~ get_command }
get_command = ${ ^"get" }
SINGLETON = _{ "@" }
not = { "-" }

current = ${ ^"current" }
defaults = ${ ^"defaults" }

term_contains = ${ text ~ STAR }
term_end = ${ text }
term_begin = ${ text ~ STAR }
text = ${ ALPHA+ }
ALPHA = _{ 'a' .. 'z' | 'A' .. 'Z' }